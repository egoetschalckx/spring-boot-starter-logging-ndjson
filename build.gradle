plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.goetschalckx'

sourceCompatibility = 1.8

version = '0.1.1-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'net.logstash.logback:logstash-logback-encoder:4.11'
    implementation 'ch.qos.logback:logback-classic:1.0.0'

    testImplementation 'junit:junit:4.12'
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar2(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'Spring Boot Logging JSON'
                description = 'Enables JSON logging in Spring Boot applications'
                packaging = 'jar'
                url = 'https://github.com/goetschalckx/spring-boot-logging-json'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'egoetschalckx'
                        name = 'Eric Goetschalckx'
                        email = 'eric@goetschalckx.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/goetschalckx/spring-boot-logging-json.git'
                    developerConnection = 'scm:git:https://github.com/goetschalckx/spring-boot-logging-json.git'
                    url = 'https://github.com/goetschalckx/spring-boot-logging-json'
                }
            }

            artifactId 'spring-boot-logging-json'

            from components.java
            artifact sourceJar
            artifact javadocJar2
        }
    }

    repositories {
        mavenLocal()

        maven {
            //url "https://oss.sonatype.org/service/local/staging/deploy/maven2"

            //def repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'

            //url = version.endsWith('SNAPSHOT') ? snapshotRepositoryUrl : repositoryUrl
            url = snapshotRepositoryUrl

            println "Artifact Version: " + version
            println "Maven URL: " + url

            credentials {
                username project.hasProperty("ossrhUser") ? ossrhUser : ""
                password project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }

}

signing {
    sign(publishing.publications)
}

tasks.withType(Sign) {
    onlyIf {
        project.hasProperty('signing.keyId5')
    }

    doFirst {
        println "Signing artifacts..."
    }
}

test {
    finalizedBy jacocoTestReport
}
jacocoTestReport {
    dependsOn test

    reports {
        xml.enabled true
        csv.enabled true
        html.enabled true
    }
}
